#!perl

use 5.010001;
use strict;
use warnings;
use Log::ger;

use Log::ger::Output 'Screen';
use Progress::Any '$progress';
use Progress::Any::Output 'TermProgressBarColor', freq=>0.25;
use Time::HiRes qw(sleep);

my $target = $ARGV[0] // 30;
$progress->target($target);
for (1..$target) {
    $progress->update(message => "Doing item $_/$target");
    if (rand() < 0.2) {
        log_warn "A warn message at item $_";
    }
    if (rand() < 0.05) {
        log_error "An error message at item $_";
    }
    sleep 0.3;
}
$progress->finish;

# ABSTRACT: Combining progress bar and Log::ger on the terminal
# PODNAME:

=head1 SYNOPSIS

 % progress-any-eg-progressbar-04-progressbar-and-logger [target=30]


=head1 DESCRIPTION

This example shows progress bar combined with L<Log::ger> screen output
(L<Log::ger::Screen>). The message printed out will not clobber the progress bar
because the progress bar output module
(L<Progress::Any::Output::TermProgressBarColor>) contains patches to make sure
of this.
